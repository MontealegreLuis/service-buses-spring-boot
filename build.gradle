plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'jacoco'
    id 'maven-publish'
    id 'info.solidsoft.pitest' version '1.7.0'
    id 'com.github.sherter.google-java-format' version '0.9'
}

group 'com.montealegreluis'

apply from: rootProject.file('hooks.gradle')

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/montealegreluis/service-buses-middleware")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/montealegreluis/service-buses-spring-boot")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    implementation "io.vavr:vavr:0.10.4"
    api "com.montealegreluis:service-buses-middleware:${serviceBusesMiddlewareVersion}"
    api 'org.springframework:spring-context:5.3.15'
    api 'org.springframework:spring-tx:5.3.15'

    testImplementation testFixtures("com.montealegreluis:service-buses:${serviceBusesVersion}")
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'com.h2database:h2:2.1.210'
    testImplementation 'org.springframework.boot:spring-boot-autoconfigure:2.6.2'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.3'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    testFixturesImplementation 'org.springframework.boot:spring-boot-autoconfigure:2.6.2'
    testFixturesImplementation 'org.springframework.boot:spring-boot-starter-test:2.6.3'
    testFixturesImplementation 'org.springframework.boot:spring-boot-starter-data-jpa:2.6.3'
    testFixturesImplementation testFixtures("com.montealegreluis:service-buses:${serviceBusesVersion}")
}

test {
    useJUnitPlatform()
    finalizedBy("jacocoTestReport")
}

jacocoTestReport {
    dependsOn test
    reports {
        html.destination file("${buildDir}/coverage")
    }
}

pitest {
    threads = 4
    outputFormats = ['HTML']
    timestampedReports = false
    junit5PluginVersion = '0.15'
    coverageThreshold = 87
    mutationThreshold = 98
}
