plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'jacoco'
    id 'info.solidsoft.pitest' version '1.7.0'
    id 'com.github.sherter.google-java-format' version '0.9'
}

group 'com.montealegreluis'

apply from: rootProject.file('hooks.gradle')

repositories {
    mavenCentral()
    maven {
        url = uri("https://maven.pkg.github.com/montealegreluis/service-buses")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.token") ?: System.getenv("TOKEN")
        }
    }
}

dependencies {
    implementation "io.vavr:vavr:0.10.4"
    api 'com.montealegreluis:service-buses:2.0.0'
    api 'org.springframework:spring-context:5.3.15'

    testImplementation testFixtures('com.montealegreluis:service-buses:2.0.0')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    testFixturesImplementation testFixtures('com.montealegreluis:service-buses:2.0.0')
}

test {
    useJUnitPlatform()
    finalizedBy("jacocoTestReport")
}

jacocoTestReport {
    dependsOn test
    reports {
        html.destination file("${buildDir}/coverage")
    }
}

pitest {
    threads = 4
    outputFormats = ['HTML']
    timestampedReports = false
    junit5PluginVersion = '0.15'
    coverageThreshold = 83
    mutationThreshold = 98
}
